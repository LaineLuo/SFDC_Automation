*** Settings ***
Suite Setup       Run Keywords    Open Application    ${REMOTE_URL}    platformName=${PLATFORM_NAME}    PLATFORM_VERSION=${PLATFORM_VERSION}    deviceName=""    udid=${UDID}
...               browserName=${BROWSER_NAME}    bundleId=${BUNDLEID}    appType=${APPTYPE}    web.url=${web.url}
...               AND    Mobile Cases test environment initialization
Suite Teardown    Clean up mobile test environment
Library           AppiumLibrary    timeout=${TIME_OUT}
Resource          ../resource.txt

*** Variables ***
${FirstName}      pad1
${LastName}       iOS
${PhoneNum}       123456789
${REMOTE_URL}     http://172.31.162.198:4723/wd/hub
${PLATFORM_NAME}    ios
${PLATFORM_VERSION}    9.3
${DEVICE_NAME}    iphone
#ipad pro
${UDID}           7f236b90eb80091de9f9681883f197fb5cfbddbc
${BUNDLEID}       com.pwc.REX
${APPTYPE}        web
${web.url}        http://login.salesforce.com
${BROWSER_NAME}    safari
${TIME_OUT}       10S
${WAIT_STEP}      5s

*** Test Cases ***
Loop test
    : FOR    ${i}    IN RANGE    0    200
    \    ${passed}=    Run Keyword And Return Status    Create a Case
    \    Run Keyword If    ${passed}!=True    Mobile Cases test environment initialization
    \    ${passed}=    Run Keyword And Return Status    Update a Case
    \    Run Keyword If    ${passed}!=True    Mobile Cases test environment initialization
    \    ${passed}=    Run Keyword And Return Status    Retrieve a Case
    \    Run Keyword If    ${passed}!=True    Mobile Cases test environment initialization
    \    ${passed}=    Run Keyword And Return Status    Delete a Case
    \    Run Keyword If    ${passed}!=True    Mobile Cases test environment initialization
    \    Log    "Loop Iteration ${i} done!"

Create
    Run Keyword And Continue On Failure    Create a Case

Update
    Run Keyword And Continue On Failure    Update a Case

Retrieve
    Run Keyword And Continue On Failure    Retrieve a Case

Delete
    Run Keyword And Continue On Failure    Delete a Case

*** Keywords ***
Create a Case
    Navi To Case App
    Click New Button
    Wait Until Page Contains Element    xpath=//span[text()="Save"]
    #Input Name
    Wait Exists And Click Element    xpath=//span[text()="Contact Name"]/parent::*/following-sibling::div[1]/input
    Wait Exists And Click Element    xpath=//a[text()="${FirstName} ${LastName}"][1]
    #Input Case Origin
    Wait Exists And Click Element    xpath=//span[text()="Case Origin"]/parent::*/following-sibling::select[1]
    Click Element    xpath=//option[text()="Phone"]
    Click Save Button
    Wait Until Page Contains Element    xpath=//span[contains(., " was created.")]
    Wait Until Page Does Not Contain Element    //div[@class="placeholder detail "]
    Wait Until Page Does Not Contain Element    xpath=//span[contains(., " was created.")]
    Go Back Click

Update a Case
    Navi To Case App
    Click Mycases
    #Click Case
    Wait Exists And Click Element    xpath=((//a[text()="${FirstName} ${LastName}"])[1]/parent::*/parent::*//a)[1]
    Wait Entity Loading
    Click Edit Button
    #Input web phone
    Wait Exists And Click Element    xpath=//*[text()="Web Information"]/ancestor::section[1]//span[text()="Web Phone"]/../../input
    Input Text    xpath=//*[text()="Web Information"]/ancestor::section[1]//span[text()="Web Phone"]/../../input    ${PhoneNum}
    Click Save Button
    Wait Until Page Contains Element    xpath=//span[contains(., " was saved.")]
    Wait Until Page Does Not Contain Element    //div[@class="placeholder detail "]
    Wait Until Page Does Not Contain Element    xpath=//span[contains(., " was saved.")]
    Go Back Click

Retrieve a Case
    Navi To Case App
    Click Mycases
    #Click Case
    Wait Exists And Click Element    xpath=((//a[text()="${FirstName} ${LastName}"])[1]/parent::*/parent::*//a)[1]
    Wait Entity Loading
    #Check result
    Wait Until Page Contains Element    xpath=//*[text()="${FirstName} ${LastName}"]
    Go Back Click

Delete a Case
    Navi To Case App
    Click Mycases
    #Click Case
    Wait Exists And Click Element    xpath=((//a[text()="${FirstName} ${LastName}"])[1]/parent::*/parent::*//a)[1]
    Wait Entity Loading
    Click Delete Button
